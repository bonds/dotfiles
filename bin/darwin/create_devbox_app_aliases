#!/usr/bin/env fish

set install_dir "$HOME/Applications/installed via DevBox"

# remove the old aliases so we don't get duplicates
# and use 'find' instead of 'rm' to avoid getting an error when the
# directory is empty, and to make it a little safer in case I mess up
# the $install_dir variable

mkdir -p "$install_dir"
find "$install_dir" -maxdepth 1 -type f -exec rm '{}' \;

# creating new aliases too quickly after deleting the old ones results
# in MacOS appending a number i.e. 'My App 2' instead of 'My App'

sleep 1

# find all the installed Nix packages that have GUI apps and create
# proper 'Finder aliases' for them so Spotlight will index them
# because it won't index UNIX symbolic links to the Nix volume

set aliases ""
set items (echo $PATH | tr " " "\n" | grep -E "^/nix/.*/bin")

for i in $items
    set appdir (dirname $i)/Applications
    if test -d $appdir
        set app (ls $appdir)
        set fullapp $appdir/$app
        # some apps like 'mpv' come with multiple variants installed, but I just
        # want one alias for them, so I just pick the first instance to link to
        if not contains $app $aliases
            echo creating alias for $app
            osascript -e "tell application \"Finder\" to make new alias at (POSIX file \"$install_dir\") to (POSIX file \"$fullapp\")" > /dev/null
            set aliases $aliases $app
        end
    end
end
