#!/usr/bin/env fish

# find all the installed Nix packages that have GUI apps and create
# proper 'Finder aliases' for them so Spotlight will index them
# because it won't index UNIX symbolic links to the Nix volume

# don't delete and remake the aliases every time even though that'd be
# slightly easier to script, because that resets the Spotlight sort order,
# which is a worse UX

set installdir "$HOME/Applications/installed via DevBox"
set linksdir "$(devbox global path)/.devbox/nix/profile/default/Applications"

set apps ""
mkdir -p "$installdir"
for link in (find $linksdir -type l)
    set apppath (realpath "$link")
    set appname (basename $apppath)
    set apps $apps $appname
    if not test -e "$installdir/$(basename $appname .app)"
        echo creating alias for $appname
        osascript -e "tell application \"Finder\" to make new alias at (POSIX file \"$installdir\") to (POSIX file \"$apppath\")" > /dev/null
    end
end

# delete all the old aliases that reference apps we don't have in our devbox
# anymore

for alias in (ls "$installdir")
    if not contains $alias.app $apps
        echo removing "$installdir/$alias"
        rm "$installdir/$alias"
    end
end